import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            //property 'mixin.debug.verbose', 'true'
            property 'mixin.debug.export', 'true'
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            mods {
                // 客戶端／伺服器 run 用 main（需要真正跑遊戲＋mixin）
                "${mod_id}" { source sourceSets.main }
            }
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
            mods { "${mod_id}" { source sourceSets.main } }
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
            mods { "${mod_id}" { source sourceSets.main } }
        }

        data {
            workingDirectory project.file('run')

            property 'survivorsdelight.datagen', 'true'
            property 'mixin.env.disable',        'true'
            property 'mixin.env.ignoreRequired', 'true'
            property 'mixin.env.disableRefMap',  'true'

            //property 'forge.logging.markers', 'LOADING,SCAN'

            args '--mod', mod_id,
                    '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/')

            mods {
                "${mod_id}" { source sourceSets.main }
            }
        }
    }
}

mixin {
    add    sourceSets.main, "${mod_id}.mixins.refmap.json"  // ★ refmap 檔名
    config "${mod_id}.mixins.json"                           // ★ 你的設定檔
    debug.verbose = true
    debug.export  = true
}

// === Lang 合併設定 ===
def MODID = mod_id                                // 直接用你現有的 mod_id 屬性
def generatedLangDir = file("src/generated/resources/assets/${MODID}/lang")
def manualLangDir    = file("src/main/resources/assets/${MODID}/lang")
def mergedDir        = file("$buildDir/mergedResources")

// 自動蒐集 locale（union）
def collectLocales = {
    def names = [] as Set
    if (generatedLangDir.exists()) {
        generatedLangDir.eachFileMatch(~/.*\.json/) { f -> names.add(f.name) }
    }
    if (manualLangDir.exists()) {
        manualLangDir.eachFileMatch(~/.*\.json/) { f -> names.add(f.name) }
    }
    return names.toList().sort()
}

// 合併規則：手寫覆蓋 generated（想反過來就把 putAll 順序對調）
tasks.register('mergeLang') {
    onlyIf { manualLangDir.exists() || generatedLangDir.exists() }
    shouldRunAfter 'runData'
    inputs.files(
            fileTree(dir: manualLangDir,    include: '**/*.json'),
            fileTree(dir: generatedLangDir, include: '**/*.json')
    )
    outputs.dir(mergedDir)

    doLast {
        def locales = collectLocales()
        locales.each { locName ->
            def outFile = file("${mergedDir}/assets/${MODID}/lang/${locName}")
            outFile.parentFile.mkdirs()

            Map merged = [:]
            def genFile = new File(generatedLangDir, locName)
            def manFile = new File(manualLangDir,    locName)

            if (genFile.exists()) merged.putAll((Map) new JsonSlurper().parse(genFile))
            if (manFile.exists()) merged.putAll((Map) new JsonSlurper().parse(manFile)) // 手寫覆蓋

            outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(merged))
        }
    }
}

//sourceSets.main.resources { srcDir 'src/generated/resources' }
//sourceSets.main.resources.srcDir mergedDir


tasks.named('processResources').configure {
    dependsOn 'mergeLang'
    // 先清掉可能預設加進來的來源（避免重複拷貝）

    // 原始資源：排除 lang（避免與合併檔衝突）
    from('src/main/resources') {
        exclude "assets/${MODID}/lang/*.json"
    }
    // datagen 資源：排除 lang（避免與合併檔衝突）
    from('src/generated/resources') {
        exclude "assets/${MODID}/lang/*.json"
    }
    // 合併結果：只放進 lang（或直接全放，通常 merged 只包含 lang）
    from(mergedDir) {
        include "assets/${MODID}/lang/*.json"
    }

    // 你原本就有的策略，保留
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}


repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven { url "https://maven.blamejared.com" }

    maven {
        // JEI
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}


dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")

    annotationProcessor "io.github.llamalad7:mixinextras-common:0.4.1"
    compileOnly       "io.github.llamalad7:mixinextras-common:0.4.1"
    implementation    "io.github.llamalad7:mixinextras-forge:0.4.1"
    jarJar            "io.github.llamalad7:mixinextras-forge:[0.4.1,)"

    implementation fg.deobf("curse.maven:terrafirmacraft-302973:6835820")
    implementation fg.deobf("curse.maven:patchouli-306770:4636277")
    implementation fg.deobf("curse.maven:farmers-delight-398521:6597298")
    implementation fg.deobf("curse.maven:survivors-abilities-1354890:7159384")

    compileOnly fg.deobf("curse.maven:firmalife-453394:6817407")
    compileOnly fg.deobf("curse.maven:jade-324717:6855440")
    compileOnly fg.deobf("curse.maven:tfc-tng-rosia-1327333:6890141")

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    // at runtime, use the full JEI jar for Forge
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs': "${mod_id}.mixins.json"
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

jarJar.enable()
reobf {
    create("jarJar")
}
tasks.named('jar').configure { finalizedBy 'reobfJar' }
tasks.named('jarJar').configure { finalizedBy 'reobfJarJar' }


