plugins {
    id 'java-library'
    id 'net.neoforged.moddev' version '2.0.107'
    id 'idea'
}

group = rootProject.group
version = rootProject.version

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

// 讓 Gradle 在設定 dev 時，確保另外兩個專案已經配置好 sourceSets
evaluationDependsOn(":sd-neo")
evaluationDependsOn(":srsoil-neo")

dependencies {
    //implementation "curse.maven:terrafirmacraft-302973:7476548" //18
    //implementation "curse.maven:terrafirmacraft-302973:7452541"//17 => BREAKING
    implementation "curse.maven:terrafirmacraft-302973:7343990" //16
    //implementation "curse.maven:terrafirmacraft-302973:7328217" //10
    implementation "curse.maven:patchouli-306770:6842247"
    implementation "curse.maven:farmers-delight-398521:6917256"
    implementation "curse.maven:survivors-abilities-1354890:7218876"

    //implementation "curse.maven:firmalife-453394:7448866"
    implementation "curse.maven:firmalife-453394:7498010"

    implementation "curse.maven:beneath-1113980:7503901"
    implementation "curse.maven:my-nethers-delight-1003673:6938550"

    compileOnly "curse.maven:jade-324717:6853386"
    runtimeOnly "curse.maven:jade-324717:6853386"

    // JEI
    compileOnly "mezz.jei:jei-1.21.1-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-1.21.1-neoforge-api:${jei_version}"
    runtimeOnly  "mezz.jei:jei-1.21.1-neoforge:${jei_version}"

    implementation "curse.maven:ihearttfc-1118578:7455936"

    // 這兩行的目的不是「dev 要 compile 它們的 API」
    // 而是把它們的輸出與相依帶進 dev 的 runtime classpath，跑起來最穩
    runtimeOnly project(":sd-neo")
    runtimeOnly project(":srsoil-neo")
    runtimeOnly project(":heart-neo")
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    mods {
        "survivorsdelight" {
            sourceSet(project(":sd-neo").sourceSets.main)
        }

        "survivorsrichsoils" {
            sourceSet(project(":srsoil-neo").sourceSets.main)
        }
    }

    runs {

        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', 'dev'
            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
            systemProperty 'mixin.debug.export', 'true'
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', 'dev'
            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
            systemProperty 'mixin.debug.export', 'true'
        }

        data {
            data()
            // 你要不要集中 datagen 看你的工作流；集中也可以，但要指定 --mod 多個
            // programArguments.addAll '--mod', '<modid1>', '--mod', '<modid2>', '--all', ...
            systemProperty 'mixin.env.ignoreRequired', 'true'
            systemProperty 'mixin.env.remapRefMap', 'true'
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG

            // 你想要集中 run dir，就在 dev 統一
            gameDirectory = rootProject.file("run/1.21.1-neoforge")
        }
    }
}
